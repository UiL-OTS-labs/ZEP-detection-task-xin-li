/******************************************************************************\
FILE:           task.zm
AUTHOR:         Theo Veenker <theo.veenker@beexy.nl>
ADAPTED BY:     -

DESCRIPTION:

Implementation of task: Same-Different Auditory Discrimination (AX)
See readme.txt for more information.


Global structure of this experiment phase:

  - instructions

  - practice part
      trial

  - pause

  - test part
      trial


HISTORY:
2012-01-30 TV   Created.
2016-05-09 CvR   Adjusted (C.P.A.vanrun@uu.nl)

\******************************************************************************/

requires 1.12;

import defs;
import shuffle;
import output;

Block task_test
{
    on_event:entry()
    {
         task_test.num_cycles = 3;
    }

    Part
    {
        on_event:entry()
        {
            blank_page.action_auto(this, 500ms);
        }
    }

    Block test
    {
        TestItem        item;   // current item
        TestItem[..] test_items_block_copy;

        on_event:entry()
        {
            create_shallow_copy_test_items(test_items_block_copy);

            // Randomize test items table.
            if (shuffle_test_items(test_items) != OK) terminate;

            setup();
        }

        void create_shallow_copy_test_items(TestItem[] target)
        {
            target.size = 0;
            target.size = test_items.size;
            int index;
            while(index < test_items.size)
            {
                target[index] = test_items[index];
                index++;
            }
        }

        void setup()
        {   
            test.num_cycles = test_items_block_copy.size;

            // Prepare test page: progress bar enabled, feedback and prompt 
            // enabled.
            test_page.setup(num_cycles, true, true);
            test_page_overlay.only_show_question();
        }

        on_event:exit()
        {
            test_page.cleanup();

            // Save test output table as CSV file.
            test_output_save();
        }


        Trial trial
        {
            on_event:entry()
            {
                item = test_items_block_copy[test.cycle];
                test_page.action(this, item, reference_time, test.cycle);
            }

            on_event:exit()
            {
                // Add new row to test output table.
                TestOutput output;
                output.trialnum = test.cycle + 1;
                output.id = item.id;
                output.block = task_test.cycle + 1;
                output.sndfn1 = item.sndfn1;
                output.sndfn2 = item.sndfn2;
                output.value = test_page.response.value;
                output.correctness = test_page.response.correctness;
                output.rt = test_page.response.rt;

                test_output_append(output);

                if(missed_current_item())
                {
                    test_items_block_copy.size = test_items_block_copy.size+1;
                    test_items_block_copy[test_items_block_copy.size-1] = item;
                    test.setup();
                }
            }

            bool missed_current_item()
            {
                return test_page.response.correctness == 0;
            }
        }
    }

    Part
    {
        on_event:entry()
        {
            blank_page.action_auto(this, 500ms);
        }
    }

    Part pause
    {
        on_event:entry()
        {
            pause_page.set_text(
                GENERAL_PAUSE_TEXT, 
                true);
            if(task_test.cycle != task_test.num_cycles-1)
            {
                pause_page.action(this);
                control.button_box.enable_all_buttons(CONTINUE);
            }
            else
                signal(this, CONTINUE);

            
        }
    }
}


void action(Part caller)
{
    // Activate the task. When the task is done, the registered caller
    // (c.q. the main experiment object) will automatically be signalled.
    task_test.enter(caller);
}
